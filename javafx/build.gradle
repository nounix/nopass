plugins {
    // Apply the application plugin to add support for building an application
    id 'application'
    // Javafx
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.61'

    // Javafx
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

// Define the main class for the application
mainClassName = 'ipass.App'

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes 'Main-Class': 'ipass.Launcher'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

task gen() {
    doLast {
        exec {
            executable "sh"
            args "-c", "cat ./util/stub.sh ./build/libs/*.jar > app && chmod +x app"
        }
    }
}
